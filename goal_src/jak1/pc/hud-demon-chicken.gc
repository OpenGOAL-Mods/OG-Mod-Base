;;-*-Lisp-*-
(in-package goal)

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; hud-demon-chicken
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

(deftype hud-demon-chicken (hud)
  ((ready? symbol)
   (kicked? symbol)
   (dist float)
   (grace-period time-frame)
   (demon-state symbol)
   )
  (:methods
    (make-icon (_type_ int) none)
    (kill-icon (_type_ int) none)
    (kill-all-icons (_type_) none)
    (update-display-status (_type_ int symbol) none)
    )
  )

(deftype hud-demon-chicken-parts (structure)
  ((parts (pointer hud-demon-chicken))
   )
  )

(define *hud-demon-chicken* (new 'static 'hud-demon-chicken-parts :parts #f))

(defmethod deactivate hud-demon-chicken ((this hud-demon-chicken))
  (if (and (-> *hud-demon-chicken* parts) (= (ppointer->process (-> *hud-demon-chicken* parts)) this))
      (set! (-> *hud-demon-chicken* parts) (the (pointer hud-demon-chicken) #f))
      )
  ((method-of-type hud deactivate) this)
  (none)
  )


(defmethod kill-icon hud-demon-chicken ((this hud-demon-chicken) (idx int))
  (when (and (nonzero? (-> this icons idx)) (nonzero? (-> this icons idx icon)) (-> this icons idx icon))
    (deactivate (ppointer->process (-> this icons idx icon)))
    (set! (-> this icons idx icon) #f)
    )
  (none)
  )

(defmethod kill-all-icons hud-demon-chicken ((this hud-demon-chicken))
  (dotimes (i (-> this nb-of-icons))
    (kill-icon this i)
    )
  (none)
  )

(defmethod make-icon hud-demon-chicken ((this hud-demon-chicken) (idx int))
  (kill-icon this 0)
  (hud-pc-replace-icon this 0 :skel *sidekick-sg* :z (meters 0.02))
  (none)
  )

(defmethod update-display-status hud-demon-chicken ((this hud-demon-chicken) (icon-idx int) (trigger-force symbol))
  (cond
    ((hidden? this)
     (when (-> this ready?)
       (set! (-> this kicked?) #f)
       (send-event this 'show)
       )
     (when (and (not (-> this ready?)) (not *progress-process*))
       (set! (-> this icons icon-idx icon-x) (the int (* 0.84 512)))
       (set! (-> this icons icon-idx icon-y) (the int (* 0.51 448)))
       (set! (-> this icons icon-idx scale-x) 0.004)
       (set! (-> this icons icon-idx scale-y) (* (-> this icons icon-idx scale-x) (/ -512.0 448.0)))
       (make-icon this icon-idx)
       (let ((icon0 (the manipy (ppointer->process (-> this icons icon-idx icon)))))
         (case (-> this demon-state)
           (('demon-chicken-chase)
            (send-event icon0 'art-joint-anim "sidekick-free-walk" 0)
            (send-event icon0 'rot-quat (quaternion-axis-angle! (new-stack-quaternion0) 0.0 1.0 0.15 (degrees 210)))
            )
           (('demon-chicken-dormant 'demon-chicken-idle)
            (send-event icon0 'art-joint-anim "sidekick-stance-loop" 0)
            (send-event icon0 'rot-quat (quaternion-axis-angle! (new-stack-quaternion0) 0.0 1.0 0.15 (degrees 210)))
            )
           )
         (send-event icon0 'draw #f)
         )
       (true! (-> this ready?))
       )
     )
    (else
     ;; we want to be shown!
     (false! (-> this ready?))

     ;; progress is open, let's leave.
     (when (and (not (-> this kicked?)) (= *master-mode* 'progress))
         (true! (-> this kicked?))
         (send-event this 'hide-quick)
         )

     ;; update icon anims
     (let ((icon0 (the manipy (ppointer->process (-> this icons icon-idx icon)))))
       (case (-> this demon-state)
         (('demon-chicken-chase)
          (send-event icon0 'art-joint-anim "sidekick-free-walk" 0)
          (send-event icon0 'rot-quat (quaternion-axis-angle! (new-stack-quaternion0) 0.0 1.0 0.15 (degrees 210)))
          )
         (('demon-chicken-dormant 'demon-chicken-idle)
          (send-event icon0 'art-joint-anim "sidekick-stance-loop" 0)
          (send-event icon0 'rot-quat (quaternion-axis-angle! (new-stack-quaternion0) 0.0 1.0 0.15 (degrees 210)))
          )
         )
       )

     ;; set the trigger time so we don't automatically go away
     (if (and trigger-force (!= (-> this next-state name) 'hud-leaving))
         (set! (-> this trigger-time) (current-time)))
     )
    )
  (none)
  )

(defmethod hud-update hud-demon-chicken ((this hud-demon-chicken))
  (when (and (not (-> this ready?)) (hidden? this))
    (kill-all-icons this)
    )
  (let ((demon (aif *demon-chicken-manager* (the demon-chicken (handle->process (-> it 0 demon))) (the demon-chicken #f))))
    (cond
      (demon
       (set! (-> this demon-state) (-> demon next-state name))
       (set! (-> this grace-period) (-> demon grace-period))
       (set! (-> this dist) (dist-to-target demon))
       (update-display-status this 0 #t)
       )
      (else
        ;; we have nothing. kill everything.
        (unless (hidden? this)
          (false! (-> this ready?))
          (cond
            ((!= (-> this next-state name) 'hud-leaving) 
             (send-event this 'hide)
             )
            (else
             (kill-all-icons this)
             (go hud-hidden)
             )
            )
          )
        )
      )
    )
  (none)
  )

(defmethod draw-hud hud-demon-chicken ((this hud-demon-chicken))
  ((method-of-type hud draw-hud) this)
  (with-dma-buffer-add-bucket ((buf (-> (current-frame) global-buf))
                               (bucket-id debug))
    (let ((str-x (+ (-> this text-x) (* (-> this x-sgn) (-> this offset))))
          (str-y (/ (* (+ (-> this text-y) (* (-> this y-sgn) (-> this offset)) (-> this y-offset))
                       (the int (-> *video-parms* relative-y-scale)))
                    2))
          (color (cond
                   ((<= (-> this dist) (meters 5)) (font-color red))
                   ((<= (-> this dist) (meters 10)) (font-color yellow))
                   (else (font-color white))
                   ))
          )
      (case (-> this demon-state)
          (('demon-chicken-chase)
           (draw-string-xy (string-format "~D" (the int (/ (-> this dist) (meters 1))))
                           buf str-x str-y color (font-flags shadow kerning large middle))
           )
          (('demon-chicken-idle)
           (draw-string-xy (string-format "IDLE" (-> this grace-period))
                           buf str-x str-y (font-color green) (font-flags shadow kerning large middle))
           )
          ;; (('demon-chicken-dormant)
          ;;  (draw-string-xy "" buf str-x str-y (font-color white) (font-flags shadow kerning large middle))
          ;; )
        )
      )
    )
  (none)
  )


(defmethod init-particles! hud-demon-chicken ((this hud-demon-chicken) (arg0 int))
  (hud-pc-make-icon this HUD_ICON_COUNT :skel *sidekick-sg*
                                        :x (the int (* 0.84 512)) :y (the int (* 0.51 448)) :z (meters 0.5)
                                        :scale-x 0.004 :scale-y (* -0.004 (/ 512.0 448.0)))
  (set! (-> this text-x) (the int (* 0.84 512)))
  (set! (-> this text-y) (the int (* 0.5 448)))
  (set! (-> this x-sgn) 1)
  (set! (-> this y-sgn) 0)
  (set! (-> this increment-on-event) #t)
  (set-pos-and-scale this (= (get-aspect-ratio) 'aspect16x9) (= (get-video-mode) 'pal))
  (none)
  )

(defun deactivate-demon-chicken-hud ()
  (if (-> *hud-demon-chicken* parts)
    (deactivate (ppointer->process (-> *hud-demon-chicken* parts)))
    )
  )

(defun activate-demon-chicken-hud ((tree process-tree))
  (deactivate-demon-chicken-hud)
  (set! (-> *hud-demon-chicken* parts) (process-spawn hud-demon-chicken :init hud-init-by-other 0
                                                                      :from *pc-dead-pool* :to tree))
  )

(activate-demon-chicken-hud *display-pool*)